plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.gradleup.shadow") version '8.3.6'
}

group = 'dev.war'
version = '0.2.1'
ext.apiVersion = '1.20'

repositories {
    gradlePluginPortal()
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.20-R0.1-SNAPSHOT")
    implementation 'org.mindrot:jbcrypt:0.4'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.24.1'
}

tasks.named('jar') {
    archiveClassifier.set('thin')
}

tasks.named('shadowJar') {
    archiveClassifier.set('')
    relocate 'org.mindrot.jbcrypt', 'dev.war.sentinel.libs.bcrypt'
}

tasks.named('runServer') {
    minecraftVersion("1.20")
}

tasks.build {
    dependsOn shadowJar
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version, apiVersion: apiVersion]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
